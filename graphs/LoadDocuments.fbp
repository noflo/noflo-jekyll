# This graph loads a list of documents from a directory and sends it out 
# for processing.
#
# The directory inport accepts a path for a folder where to start loading
INPORT=Directory.IN:DIRECTORY
# The subdirectory outport is where the path each subfolder is sent
OUTPORT=SubDirectory.OUT:SUBDIRECTORY
# The document outport is where the path of each regular file is sent
OUTPORT=FileName.OUT:DOCUMENT
# The error outport is where errors get sent
OUTPORT=Errors.OUT:ERROR

# @runtime noflo-nodejs

Directory(core/Split) OUT -> SOURCE Dir(filesystem/ReadDir)

Dir OUT -> IN Stat(filesystem/Stat)

# Routing rules
'isDirectory' -> ROUTES MatchOnlyDirectories(routers/GroupRouter)
'isDirectory' -> KEY GroupWithIsDirectory(groups/GroupByObjectKey)
Stat OUT -> IN DropGroups(groups/RemoveGroups)
DropGroups OUT -> IN GroupWithIsDirectory OUT -> IN MatchOnlyDirectories

# First route: subfolder
'path=^(?:(?!\.git|_(?!(includes|layouts|posts))|node_modules).)*$' -> REGEXP FilterDirs(objects/FilterPropertyValue)
'path' -> KEY SubDirectory(objects/GetObjectKey)
'false' -> SENDGROUP SubDirectory
MatchOnlyDirectories OUT -> IN FilterDirs OUT -> IN SubDirectory

# Missed routing: regular file
'path' -> KEY FileName(objects/GetObjectKey)
'false' -> SENDGROUP FileName
MatchOnlyDirectories MISSED -> IN FileName

# Error handling
Dir ERROR -> IN Errors(core/Merge)
Stat ERROR -> IN Errors
