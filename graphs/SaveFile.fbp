EXPORT=SOURCE.IN:SOURCE
EXPORT=DESTINATION.IN:DESTINATION
EXPORT=MAPFILENAME.IN:IN
EXPORT=SAVE.OUT:OUT
EXPORT=ERRORS.OUT:ERROR

'=' -> DELIMITER CompileReplacement(CompileString)
Source(Split) OUT -> STRING HoldSource(SendString) OUT -> IN CompileReplacement()
Destination(Split) OUT -> IN HoldSource()
Destination(Split) OUT -> IN CompileReplacement()
CompileReplacement() OUT -> REGEXP MapFileName(MapGroup)

# Convert some filename endings
# TODO: Move to main control
'\.md=.html' -> REGEXP MapFileName()
'\.markdown=.html' -> REGEXP MapFileName()

# Get the new filename
MapFileName() OUT -> IN ReadPath(ReadGroup)

# Create folders for the files
ReadPath() GROUP -> IN NewDir(DirName)
NewDir() OUT -> IN ThrottleDir(Throttle) OUT -> IN MkDir(MakeDir)
MkDir() LOAD -> LOAD ThrottleDir()

# Save files when their directories are ready
ReadPath() OUT -> COLLECT NewFiles(filesystem/DirectoryGroupBuffer)
MkDir() OUT -> RELEASE NewFiles()
NewFiles() OUT -> IN SplitFile(Split)
SplitFile() OUT -> IN ReadName(ReadGroup) GROUP -> FILENAME Save(WriteFile)
SplitFile() OUT -> IN Save()

# Error handling
MkDir() ERROR -> IN Errors(Merge)
Save() ERROR -> IN Errors(Merge)
