# This graph reads the _config.yml file from the given directory, parses it, and sends the configuration values out

# @runtime noflo-nodejs

# The directory inport accepts a path for a folder where to look for a 
# configuration file
INPORT=Directory.IN:DIRECTORY
# The config output is where the object containing configuration keys and
# values is sent
OUTPORT=Parse.OUT:CONFIG
OUTPORT=Errors.OUT:ERROR

# Build filename, /path/to/source/_config.yml
'_config.yml' -> STRING BuildName(strings/SendString)
Directory(core/Split) OUT -> DIRECTORY MakePath(filesystem/JoinPath)
Directory OUT -> IN BuildName OUT -> FILE MakePath

# Read and parse
MakePath OUT -> IN Read(filesystem/ReadFile)
Read OUT -> IN Parse(yaml/ParseYaml)

# Error handling
Read ERROR -> IN Errors(core/Merge)
Parse ERROR -> IN Errors
